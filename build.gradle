plugins {
    id 'maven-publish'
    id 'java-library'
    id 'distribution'

}

group 'vcs.odr.opendrive4j'
version '1.0.0'
description 'A Java mapping for ASAM e.V. OpenDRIVE standard'
ext.date = new Date()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

repositories {
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    api 'org.xmlobjects:xml-objects:1.1.1'
    api 'org.xmlobjects.gml:gml-objects:1.1.1'
    api 'org.locationtech.jts:jts-core:1.20.0'
    annotationProcessor 'org.xmlobjects:xml-objects:1.1.1'
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
    }
}

distributions.main.contents {
    from 'README.md'
    into('lib') {
        from jar
        from configurations.compileClasspath
    }
    into('license') {
        from 'LICENSE'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/virtualcitySYSTEMS/opendrive4j'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'sindram'
                        name = 'Maximilian Sindram'
                        email = 'msindram@vc.systems'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                    developer {
                        id = 'rruhdorfer'
                        name = 'Roland Ruhdorfer'
                        email = 'rruhdorfer@vc.systems'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/virtualcitySYSTEMS/opendrive4j'
                    connection = 'scm:git:https://github.com/virtualcitySYSTEMS/opendrive4j.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/DEIN_GITHUB_USERNAME/DEIN_REPOSITORY_NAME")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}



